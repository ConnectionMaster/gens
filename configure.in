AC_INIT(configure.in)

AC_PREREQ(2.5)
AC_CONFIG_HEADER(config.h)
AC_CANONICAL_BUILD
AC_CANONICAL_HOST
AC_CANONICAL_TARGET
AC_CONFIG_SRCDIR([configure.in])
AM_INIT_AUTOMAKE(gens, 2.15.5/GS-r6+)
m4_defun([_LT_AC_LANG_F77_CONFIG], [:])

# libtool
AC_LIBTOOL_DLOPEN
AC_ENABLE_SHARED
AC_DISABLE_STATIC

# Set up libtool.
AC_PROG_LIBTOOL

AC_PROG_CC
AM_PROG_CC_C_O
AC_PROG_CXX
AM_PROG_AS
AC_ISC_POSIX
AC_HEADER_STDC

# Work around autoconf's default "-g -O2" CFLAGS and CXXFLAGS.
if test "$CFLAGS" = "-g -O2"; then
	CFLAGS=""
fi
if test "$CXXFLAGS" = "-g -O2"; then
	CXXFLAGS=""
fi

# Check operating system.
nasm_debug=""
yasm_debug=""
case $host in
	i?86*mingw32*)
		os=win32;
		obj_format=win32;
		AC_DEFINE(GENS_OS_WIN32, 1, Win32 operating system)
		nasm_debug="-g"
		yasm_debug="-g stabs";
		cflags_debug="-gstabs"
		;;
	i?86*cygwin*)
		os=win32;
		obj_format=win32;
		AC_DEFINE(GENS_OS_WIN32, 1, Win32 operating system)
		cflags_win32="-mno-cygwin -mwin32 ${cflags_win32}";
		nasm_debug="-g"
		yasm_debug="-g stabs";
		cflags_debug="-gstabs"
		;;
	i?86*darwin*)
		os=macosx;
		obj_format=macho;
		AC_DEFINE(GENS_OS_MACOSX, 1, MacOS X operating system)
		AC_DEFINE(GENS_OS_UNIX, 1, Unix operating system)
		nasm_debug="-g -F null";
		yasm_debug="-g null";
		cflags_debug="-ggdb"
		;;
	i?86*linux*)
		os=linux;
		obj_format=elf32;
		AC_DEFINE(GENS_OS_LINUX, 1, Linux operating system)
		AC_DEFINE(GENS_OS_UNIX, 1, Unix operating system)
		nasm_debug="-g -F dwarf";
		yasm_debug="-g dwarf2";
		cflags_debug="-ggdb"
		;;
	i?86*)
		os=unix;
		obj_format=elf32;
		AC_DEFINE(GENS_OS_UNIX, 1, Unix operating system)
		nasm_debug="-g -F dwarf";
		yasm_debug="-g dwarf2";
		cflags_debug="-ggdb"
		;;
	x86_64*)
		obj_format=elf64;
		AC_MSG_ERROR([64-bit is currently not supported.]);
		nasm_debug="-g -F dwarf";
		yasm_debug="-g dwarf2";
		cflags_debug="-ggdb"
		;;
	*)
		AC_MSG_ERROR([Unsupported system architecture: $host]);
		;;
esac

# Operating system
AM_CONDITIONAL([GENS_OS_LINUX],  [test x$os = xlinux])
AM_CONDITIONAL([GENS_OS_MACOSX], [test x$os = xmacosx])
AM_CONDITIONAL([GENS_OS_UNIX],   [test x$os = xlinux -o x$os = xmacosx -o x$os = xunix])
AM_CONDITIONAL([GENS_OS_WIN32],  [test x$os = xwin32])

# Linker format
AM_CONDITIONAL([GENS_OBJ_WIN32], [test x$obj_format = xwin32])
AM_CONDITIONAL([GENS_OBJ_ELF32], [test x$obj_format = xelf32])
AM_CONDITIONAL([GENS_OBJ_ELF64], [test x$obj_format = xelf64])
AM_CONDITIONAL([GENS_OBJ_MACHO], [test x$obj_format = xmacho])

# Enable C99 mode.
# (Disabled on Win32 due to some weird issues.)
if test x$os != xwin32; then
	AC_PROG_CC_C99
fi

# Debugging symbols and warnings.
AC_ARG_ENABLE(debug, AC_HELP_STRING([--enable-debug], [Build with debugging symbols. [[default=yes]]]), enable_debug=$enableval, enable_debug=yes)
if test x$enable_debug = xyes -o x$enable_debug = xfull; then
	# Debugging symbols enabled.
	cflags_optimization="-O0"
	AC_DEFINE(GENS_DEBUG, 1, Enable debugging)	
else
	# Debugging disabled.
	nasm_debug=""
	yasm_debug=""
	cflags_debug=""
	cflags_optimization="-O2"
fi
AM_CONDITIONAL([GENS_DEBUG], [test x$enable_debug = xyes -o x$enable_debug = xfull])

# BEGIN: Unix/Linux (and MacOS X for now)

if test x$os = xunix -o x$os = xlinux -o x$os = xmacosx; then

# Linux allows UI selection.
# TODO: Re-enable this eventually.
#AC_ARG_ENABLE(ui, AC_HELP_STRING([--enable-ui=[[ui]]], [Enable either the 'gtk' or 'qt4' UI [[default=gtk]]]), ui=$enableval, ui=gtk)
#if test x$ui != xgtk && test x$ui != xqt4; then
#	ui=gtk
#fi
ui=gtk

# Check which UI was selected.

if test x$ui = xgtk; then

# GTK+
GTK_VERSION=2.4.0
AM_PATH_GTK_2_0($GTK_VERSION,
		:,
		AC_MSG_ERROR([*** GTK+ version $GTK_VERSION or later not found!])
)

AC_DEFINE(GENS_UI_GTK, 1, Enable the GTK+ UI)

# TODO: Disable deprecated GTK+ features.
GTK_CFLAGS="$GTK_CFLAGS -DGTK_DISABLE_DEPRECATED -DDISABLE_DEPRECATED -DGSEAL_ENABLE"

#elif test x$ui = xqt4; then
#
# Qt4
#PKG_CHECK_MODULES([Qt4], QtCore >= 4.2.0 QtGui >= 4.2.0)
#AC_DEFINE(GENS_UI_QT4, 1, Enable the Qt4 UI)
#Qt4_moc_location=`pkg-config QtCore --variable=moc_location`
#Qt4_uic_location=`pkg-config QtCore --variable=uic_location`
#AC_SUBST(Qt4_moc_location)
#AC_SUBST(Qt4_uic_location)
#
fi

# SDL

SDL_VERSION=1.1.3
AM_PATH_SDL($SDL_VERSION,
            :,
            AC_MSG_ERROR([*** SDL version $SDL_VERSION not found!])
)

# END: Unix / Linux (and MacOS X for now)

elif test x$os = xwin32; then

# Win32
AC_DEFINE(GENS_UI_WIN32, 1, Enable the Win32 UI)

# Check for a Win32 resource compiler.
RC=""
if test x${host_alias} != x; then
	# Check with a host alias.
	AC_PATH_PROG(RC, ${host_alias}-windres, no)
fi;
if test x$RC = x -o test x$RC = xno; then
	# Check without a host alias.
	AC_PATH_PROG(RC, windres, no)
	if test x$RC = xno; then
		AC_MSG_ERROR([*** Win32 resource compiler not found!])
	fi
fi
AC_SUBST(RC)

fi

AM_CONDITIONAL([GENS_UI_GTK], [test x$ui = xgtk])
#AM_CONDITIONAL([GENS_UI_QT4], [test x$ui = xqt4])
AM_CONDITIONAL([GENS_UI_WIN32], [test x$os = xwin32])

dnl GTK_CFLAGS="$GTK_CFLAGS -DGTK_DISABLE_DEPRECATED"

# Check for nasm.
AC_ARG_WITH(nasm, AC_HELP_STRING([--with-nasm[[=nasm]]], [Select nasm or yasm [[default=nasm]]]), with_nasm=$withval, with_nasm=nasm)

# nasm optimization level.
AC_ARG_ENABLE(nasm_opt, AC_HELP_STRING([--enable-nasm-opt[[=3]]], [Set nasm optimization level [[default=3]]]), enable_nasm_opt=$enableval, enable_nasm_opt=3)

# Set output format.
if test x$obj_format = xwin32; then
	nasm_obj_format="-f win32"
elif test x$obj_format = xwin64; then
	nasm_obj_format="-f win64"
elif test x$obj_format = xmacho; then
	nasm_obj_format="-f macho -D __GCC2"
elif test x$obj_format = xelf32; then
	nasm_obj_format="-f elf -D __GCC2"
elif test x$obj_format = xelf64; then
	nasm_obj_format="-f elf64 -D __GCC2"
fi

if test x$with_nasm != x -a x$with_nasm != xnasm -a x$with_nasm != xyasm; then
	AC_MSG_WARN([Unsupported nasm-compatible assembler $with_nasm; using nasm instead.])
	with_nasm=nasm
fi

if test x$with_nasm = xyasm; then
	# yasm specified as nasm-compatible assembler.
	AC_PATH_PROG(YASM, yasm, no)
	if test x$YASM = xno; then
		# yasm wasn't found.
		AC_MSG_ERROR([*** yasm not found!])
	fi
	
	# Set the NASM path to YASM.
	NASM="${YASM} ${nasm_obj_format} ${yasm_debug} -I\$(top_srcdir)/src/ -w"
elif test x$with_nasm = xnasm; then
	# nasm specified as nasm-compatible assembler.
	AC_PATH_PROG(NASM, nasm, no)
	if test x$NASM = xno; then
		# nasm wasn't found.
		AC_MSG_ERROR([*** nasm not found!])
	fi
	
	# nasm optimization level
	if test x$enable_nasm_opt = xyes; then
		enable_nasm_opt=3
	elif test x$enable_nasm_opt = xno; then
		enable_nasm_opt=1;
	fi
	
	# Set the NASM path to NASM.
	NASM="${NASM} -O${enable_nasm_opt} ${nasm_obj_format} ${nasm_debug} -I\$(top_srcdir)/src/ -w-orphan-labels"
fi

AC_ARG_ENABLE(x86_asm, AC_HELP_STRING([--enable-x86-asm], [Enable x86 assembly optimizations [[default=yes]]]), enable_x86_asm=$enableval, enable_x86_asm=yes)
if test x$enable_x86_asm = xyes; then
	AC_DEFINE(GENS_X86_ASM, 1, Enable x86 assembly optimizations)
fi
AM_CONDITIONAL([GENS_X86_ASM], [test x$enable_x86_asm = xyes])

AC_ARG_ENABLE(debugger, AC_HELP_STRING([--enable-debugger], [Enable the built-in debugger [[default=yes]]]), enable_debugger=$enableval, enable_debugger=yes)
if test x$enable_debugger = xyes; then
	AC_DEFINE(GENS_DEBUGGER, 1, Enable the built-in debugger)
fi
AM_CONDITIONAL([GENS_DEBUGGER], [test x$enable_debugger = xyes])

# Physical CD-ROM drive support. (Linux/Win32 only.)
AC_ARG_ENABLE(cdrom, AC_HELP_STRING([--enable-cdrom], [Enable CD-ROM drive access for SegaCD games [[default=yes]]]), enable_cdrom=$enableval, enable_cdrom=yes)
if test x$enable_cdrom = xyes; then
	if test x$os = xlinux -o x$os = xwin32; then
		AC_DEFINE(GENS_CDROM, 1, Enable CD-ROM drive access for SegaCD games)
	else
		enable_cdrom=no
		AC_MSG_WARN([Physical CD-ROM drive support is currently only available on Linux and Windows. Disabling.])
	fi
fi
AM_CONDITIONAL([GENS_CDROM], [test x$enable_cdrom = xyes])

# SegaCD MP3 support.
AC_ARG_ENABLE(mp3, AC_HELP_STRING([--enable-mp3], [Enable MP3 support for SegaCD games [[default=yes]]]), enable_mp3=$enableval, enable_mp3=yes)
if test x$enable_mp3 = xyes; then
	AC_DEFINE(GENS_MP3, 1, Enable MP3 support for SegaCD games)
fi
AM_CONDITIONAL([GENS_MP3], [test x$enable_mp3 = xyes])

AC_ARG_ENABLE(win32_console, AC_HELP_STRING([--enable-win32-console], [Enable console output on Windows [[default=no]]]), enable_win32_console=$enableval, enable_win32_console=no)
if test x$os = xwin32; then
	if test x$enable_win32_console = xyes; then
		# Enable console output on Win32.
		AC_DEFINE(GENS_WIN32_CONSOLE, 1, Enable console output on Windows)
	fi
else
	# Not Windows - ignore this option.
	if test x$enable_win32_console = xyes; then
		# No, you can't enable a Windows console if you're not building for Windows.
		# Not yours.
		AC_MSG_WARN([Not building for Windows; ignoring --enable-win32-console])
		enable_win32_console=no
	fi
fi
AM_CONDITIONAL([GENS_WIN32_CONSOLE], [test x$enable_win32_console = xyes])

AC_ARG_WITH(opengl, AC_HELP_STRING([--without-opengl], [Build without OpenGL support [[default=no]]]), with_opengl=$withval, with_opengl=yes)
if test x$with_opengl = xyes; then
	AC_MSG_CHECKING(for OpenGL support)
	with_opengl=no
	if test x$os = xwin32; then
		AC_MSG_WARN([OpenGL support is not currently available on Win32. Disabling.])
	else
		AC_TRY_COMPILE([
			#ifndef __APPLE__
			#include <GL/gl.h>
			#else
			#include <OpenGL/gl.h>
			#endif
		],[
			GLint g;
		],[
			with_opengl=yes
		])      
		AC_MSG_RESULT($with_opengl)
		if test x$with_opengl = xyes; then
			if test x$os = xmacosx; then
				LDFLAGS="$LDFLAGS -framework OpenGL"
			else
				LDFLAGS="$LDFLAGS -lGL"
			fi
			AC_DEFINE(GENS_OPENGL, 1, OpenGL support)
		fi
	fi
fi
AM_CONDITIONAL([GENS_OPENGL], [test x$with_opengl = xyes])


###
# zlib
###

AC_ARG_WITH(zlib, AC_HELP_STRING([--without-zlib], [Build without zlib support [[default=no]]]), with_zlib=$withval, with_zlib=yes)
AC_ARG_WITH(internal_zlib, AC_HELP_STRING([--with-internal-zlib], [Use internal zlib [[default=no]]]), with_internal_zlib=$withval, with_internal_zlib=no)

# If internal zlib was enabled, but zlib was disabled, enable zlib.
# The user may have thought that the plain zlib option was for shared zlib.
if test x$with_internal_zlib = xyes; then
	with_zlib=yes
fi

if test x$with_zlib = xno; then
	# zlib disabled; disable internal zlib library.
	with_internal_zlib=no
else
	# zlib enabled; check if internal zlib library is eanbled.
if test x$with_internal_zlib = xno; then
AC_MSG_CHECKING(for system zlib)
	with_zlib=no
	AC_TRY_COMPILE([
		#include <zlib.h>
	],[
		gzFile gzfd;
	],[
		with_zlib=yes
	])
	AC_MSG_RESULT($with_zlib)
	if test x$with_zlib = xno; then
		# External zlib not found. Use the internal version.
		with_zlib=yes
		with_internal_zlib=yes
	fi
fi

fi

if test x$with_zlib = xyes; then
AC_DEFINE(GENS_ZLIB, 1, Compression support via zlib)
if test x$with_internal_zlib = xyes; then
AC_DEFINE(GENS_ZLIB_INTERNAL, 1, Use internal zlib instead of system zlib)
fi # GENS_ZLIB_INTERNAL
fi # GENS_ZLIB
AM_CONDITIONAL([GENS_ZLIB], [test x$with_zlib = xyes])
AM_CONDITIONAL([GENS_ZLIB_INTERNAL], [test x$with_internal_zlib = xyes])


###
# libpng
###

AC_ARG_WITH(png, AC_HELP_STRING([--without-png], [Build without PNG support [[default=no]]]), with_png=$withval, with_png=yes)
AC_ARG_WITH(internal_png, AC_HELP_STRING([--with-internal-png], [Use internal libpng [[default=no]]]), with_internal_png=$withval, with_internal_png=no)

# If internal libpng was enabled, but PNG was disabled, enable PNG.
# The user may have thought that the plain libpng option was for shared libpng.
if test x$with_internal_png = xyes; then
	with_png=yes
fi

if test x$with_png = xno; then
	# libpng disabled; disable internal libpng library.
	with_internal_png=no
else
	# libpng enabled; check if internal libpng library is eanbled.
if test x$with_internal_png = xno; then
AC_MSG_CHECKING(for system libpng)
	with_png=no
	AC_TRY_COMPILE([
		#include <png.h>
	],[
		png_structp png_ptr;
	],[
		with_png=yes
	])
	AC_MSG_RESULT($with_png)
	if test x$with_png = xno; then
		# External libpng not found. Use the internal version.
		with_png=yes
		with_internal_png=yes
	fi
fi

fi

if test x$with_png = xyes; then
	AC_DEFINE(GENS_PNG, 1, PNG support via libpng)
	if test x$with_internal_png = xyes; then
		AC_DEFINE(GENS_PNG_INTERNAL, 1, Use internal libpng instead of system libpng)
	fi # GENS_PNG_INTERNAL
fi # GENS_PNG

AM_CONDITIONAL([GENS_PNG], [test x$with_png = xyes])
AM_CONDITIONAL([GENS_PNG_INTERNAL], [test x$with_internal_png = xyes])


###
# LZMA / 7z. (internal only)
###

AC_ARG_WITH(7z, AC_HELP_STRING([--without-7z], [Build without internal 7z support [[default=no]]]), with_7z=$withval, with_7z=yes)
if test x$with_7z = xyes; then
	AC_DEFINE(GENS_LZMA, 1, LZMA/7z support via internal LZMA SDK)
fi # GENS_LZMA

AM_CONDITIONAL([GENS_LZMA], [test x$with_7z = xyes])


###
# gcc -fvisibility-inlines-hidden option
###

AC_MSG_CHECKING(for gcc -fvisibility-inlines-hidden support)

if test x$obj_format = xwin32; then
	# Symbol visibility isn't applicable on Win32.
	AC_MSG_RESULT(not applicable on Win32)
else
	AC_COMPILE_IFELSE(
		AC_LANG_PROGRAM(
			[[
				#include <stdio.h>
				#if defined(__GNUC__) && (__GNUC__ >= 4)
					// gcc-4.x supports visibility settings.
				#else
					// Not gcc-4.x or higher.
					#error Not gcc-4.x or higher.
				#endif
			]],
			[[printf("Test for gcc visibility support.\n");]]),
		[fvisibility=yes],
		[fvisibility=no]
	)
	
	AC_MSG_RESULT($fvisibility)
	
	if test x$fvisibility = xyes; then
		cxxflags_visibility="-fvisibility-inlines-hidden"
	else
		cxxflags_visibility=""
	fi
fi


###
# C++ map type
###

AC_MSG_CHECKING(for hash table type)

# These tests use C++.
AC_LANG(C++)

# Check if the TR1 unordered map works. (gcc-4.x and later)
AC_COMPILE_IFELSE(
	AC_LANG_PROGRAM(
		[[#include <tr1/unordered_map>]],
		[[std::tr1::unordered_map<int, int>::iterator testIterator;]]),
	[hashtable=tr1_unordered_map],
	[hashtable=no]
	)

if test x$hashtable = xno; then
	# Check if the gcc hash map works. (gcc-3.x and earlier)
	AC_COMPILE_IFELSE(
		AC_LANG_PROGRAM(
			[[#include <ext/hash_map>]],
			[[__gnu_cxx::hash_map<int, int>::iterator testIterator;]]),
		[hashtable=gnu_hash_map],
		[hashtable=no]
		)
	
	if test x$hashtable = xno; then
		# Check if the standard C++ map works.
		AC_COMPILE_IFELSE(
			AC_LANG_PROGRAM(
				[[#include <map>]],
				[[std::map<int, int>::iterator testIterator;]]),
			[hashtable=std_map],
			[hashtable=no]
			)
		
		if test x$hashtable = xno; then
			AC_MSG_ERROR(Could not find a usable hash table implementation.)
		fi
	fi
fi

if test x$hashtable = xtr1_unordered_map; then
	AC_MSG_RESULT(TR1 unordered_map)
	AC_DEFINE(GENS_HASHTABLE_TR1_UNORDERED_MAP, 1, Use the TR1 unordered_map class.)
elif test x$hashtable = xgnu_hash_map; then
	AC_MSG_RESULT(GNU hash_map)
	AC_DEFINE(GENS_HASHTABLE_GNU_HASH_MAP, 1, Use the GNU hash_map class.)
elif test x$hashtable = xstd_map; then
	AC_MSG_RESULT(Standard C++ map)
	AC_MSG_WARN(SLOW std::map is being used; consider upgrading your compiler.)
	AC_DEFINE(GENS_HASHTABLE_STD_MAP, 1, Use the standard C++ std::map class. (SLOW))
fi

# Reset test language to C.
AC_LANG(C)


###
# Miscellaneous
###

# Check if the linker supports --as-needed.
AC_MSG_CHECKING(if the linker supports --as-needed)
as_needed=`$LD --help 2>&1 | grep as-needed`
if test "x$as_needed" != "x"; then
	# --as-needed is supported.
	AC_MSG_RESULT(yes)
	ldflags_as_needed="-Wl,--as-needed"
else
	# --as-needed is not supported.
	AC_MSG_RESULT(no)
	ldflags_as_needed=""
fi

# Windows-specific options.
if test x$os = xwin32; then
	# Check if the Windows console should be enabled.
	if test x$enable_win32_console = xyes; then
		# Enable the Windows console.
		ldflags_win32="$LDFLAGS -Wl,-subsystem,console"
	else
		# Disable the Windows console.
		ldflags_win32="$LDFLAGS -Wl,-subsystem,windows"
	fi
	
	# Windows NT target version and MSIE target version.
	# (Seriously, MSIE target version?)
	cflags_win32="${cflags_win32} -D_WIN32_WINNT=0x0400 -D_WIN32_IE=0x0300"
else
	# Not Win32.
	ldflags_win32=""
	cflags_win32=""
fi

# Enable -Wall and -Wextra on gcc only.
if test "x$GCC" = "xyes"; then
	cflags_warnings="-Wall -Wextra"
else
	cflags_warnings=""
fi

AM_CFLAGS="${cflags_optimization} ${cflags_debug} ${cflags_warnings} ${cflags_win32}"
AM_CXXFLAGS="${cflags_optimization} ${cflags_debug} ${cflags_warnings} ${cflags_win32} ${cxxflags_visibility}"
AM_LDFLAGS="${ldflags_as_needed} ${ldflags_win32}"

# Use package-specific libdir.
libdir="$libdir/gens"

# Check for system libraries and functions.
AC_CHECK_LIB(rt, clock_gettime)
AC_CHECK_LIB(c, getopt)
AC_FUNC_ALLOCA

# Check for various functions.
AC_CHECK_FUNCS(strdup, has_strdup=yes, has_strdup=no)
AM_CONDITIONAL([HAVE_STRDUP], [test x$has_strdup = xyes])

AC_CHECK_FUNCS(strtok_r, has_strtok_r=yes, has_strtok_r=no)
AM_CONDITIONAL([HAVE_STRTOK_R], [test x$has_strtok_r = xyes])

AC_SUBST(SDL_CFLAGS)
AC_SUBST(SDL_LIBS)
AC_SUBST(GTK_CFLAGS)
AC_SUBST(GTK_LIBS)
AC_SUBST(LIBS)
AC_SUBST(GETTIMEFLAG)

AC_SUBST(CFLAGS)
AC_SUBST(CXXFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(AM_CFLAGS)
AC_SUBST(AM_CXXFLAGS)
AC_SUBST(AM_LDFLAGS)
AC_SUBST(AM_CPPFLAGS)
AC_SUBST(libdir)

AC_CONFIG_FILES([
Makefile
images/Makefile
xdg/Makefile
src/Makefile
src/zlib/Makefile
src/minizip/Makefile
src/libpng/Makefile
src/mp3_dec/Makefile
src/starscream/Makefile
src/mdZ80/Makefile
src/lzma/Makefile
src/gens/Makefile
src/gens/plugins/render/normal/Makefile
src/gens/plugins/render/double/Makefile
src/mdp/Makefile
src/mdp/render/Makefile
src/mdp/render/scanline/Makefile
src/mdp/render/scanline_25/Makefile
src/mdp/render/scanline_50/Makefile
src/mdp/render/interpolated/Makefile
src/mdp/render/interpolated_scanline/Makefile
src/mdp/render/interpolated_scanline_25/Makefile
src/mdp/render/interpolated_scanline_50/Makefile
src/mdp/render/scale2x/Makefile
src/mdp/render/scale3x/Makefile
src/mdp/render/scale4x/Makefile
src/mdp/render/hq2x/Makefile
src/mdp/render/hq3x/Makefile
src/mdp/render/hq4x/Makefile
src/mdp/render/2xsai/Makefile
src/mdp/render/epx/Makefile
src/mdp/render/epx_plus/Makefile
src/mdp/render/super_eagle/Makefile
src/mdp/render/super_2xsai/Makefile
src/mdp/render/blargg_ntsc/Makefile
src/mdp/misc/Makefile
src/mdp/misc/game_genie/Makefile
src/mdp/misc/vdp_layer_options/Makefile
src/mdp/misc/sgens/Makefile
src/mdp/misc/ips_patcher/Makefile
])

AC_OUTPUT
